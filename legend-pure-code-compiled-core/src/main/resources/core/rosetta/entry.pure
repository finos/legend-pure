// Copyright 2020 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::generation::metamodel::*;
import meta::pure::generation::*;
import meta::external::rosetta::generation::*;

Profile meta::external::rosetta::generation::metadata
{
  stereotypes: [key, reference, scheme, id, template];
}

Class meta::external::rosetta::generation::CDMConfig extends GenerationConfiguration
{
   // TODO delete as scopeElements will take care of this
   classes: Class<Any>[*];
   enums : Enumeration<Any>[*];
}

function <<access.private, Generation.Configuration>>  meta::external::rosetta::generation::describeConfiguration():GenerationParameter[*]
{
  meta::pure::generation::describeConfiguration(CDMConfig,meta::external::rosetta::generation::defaultConfig__CDMConfig_1_,[]);
}

function <<access.private, Generation.Transformation>> meta::external::rosetta::generation::internal_generateCdm(cdmConfig:CDMConfig[1]):GenerationOutput[*]
{
   let classesOutput = $cdmConfig.classes->map(c|$c->meta::external::rosetta::generation::classToCDMString());
   let enumsOutput = $cdmConfig.enums->map(e|$e->meta::external::rosetta::generation::enumToCDMEnum());
   ^GenerationOutput(content=$classesOutput->concatenate($enumsOutput)->joinStrings('\n'), fileName='cdmTypes.txt');
}

function meta::external::rosetta::generation::generateCDMFromPureWithScope(cdmConfig:CDMConfig[1]):GenerationOutput[*]
{
   $cdmConfig.scopeElements->filter(e| !$e->instanceOf(Package))->map(e|assert($e->instanceOf(Type),' CDM generation of ' + $e->type().name->toOne()+ ' is not currently supported. Tried to generate CDM for '+ $e->elementToPath()) ;);
   let scopeElements = $cdmConfig.allPackageScopeElements()->filter(p|$p->instanceOf(Type))->cast(@Type);
   let content = $scopeElements->map(c| $cdmConfig->meta::external::rosetta::generation::generateCDMFromPure($c))->joinStrings('\n\n');
   ^GenerationOutput(content=$content, fileName='cdmTypes.txt', format = 'rosetta');
}

function meta::external::rosetta::generation::generateCDMFromPure(config:CDMConfig[1], element: Type[1]) : String[*]
{
   $element->match([
      class: Class<Any>[1]|$class->meta::external::rosetta::generation::classToCDMString();,
      enumeration: Enumeration<Any>[1]|$enumeration->meta::external::rosetta::generation::enumToCDMEnum();,
      type: Type[1]| fail($element->type()->elementToPath() + ' not supported for cdm transform'); '';
   ]);
}

function  meta::external::rosetta::generation::defaultConfig():CDMConfig[1]
{
    ^CDMConfig(classes= [],enums=[]);
}