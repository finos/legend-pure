// Copyright 2020 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::alloy::runtime::java::*;
import meta::alloy::runtime::java::graphFetch::stream::*;
import meta::alloy::runtime::java::naming::*;
import meta::java::codeGeneration::code::*;
import meta::java::codeGeneration::convention::*;
import meta::java::metamodel::factory::*;
import meta::java::metamodel::project::*;
import meta::java::tostring::*;
import meta::pure::dataQuality::*;
import meta::pure::executionPlan::engine::*;

// TODO This depends on meta::alloy::runtime::java::generateResultBasedConstructor being used for this specific purpose.  The approach should be generalized and the specifics moved to this package

function meta::alloy::runtime::java::graphFetch::stream::createStreamReading(data:GraphFetchModelToModelData[1], path:String[1], context:GenerationContext[1], debug:DebugContext[1]): Project[1]
{
   newProject()->addClasses(createStreamReadingClass($data, $path, $context, $debug));
}

function meta::alloy::runtime::java::graphFetch::stream::streamReaderClass(conventions:Conventions[1], path:String[1]): meta::java::metamodel::Class[1]
{
   $conventions->planNodeClass('public', $path, 'StreamReader');
}

function <<access.private>> meta::alloy::runtime::java::graphFetch::stream::createStreamReadingClass(data:GraphFetchModelToModelData[1], path:String[1], context:GenerationContext[1], debug:DebugContext[1]): meta::java::metamodel::Class[1]
{
   let itr  = javaField('private', javaIterator($data.sourceClass), 'itr');

   $context.conventions->streamReaderClass($path)
      ->implements($context.conventions->className(StoreStreamReader))
      ->addField(javaField('private', javaStream($data.sourceClass), 'stream'))
      ->addField($itr)
      ->addConstructor($data.sourceClass)
      ->addMethod(javaMethod('public', javaVoid(), 'initReading',    [], '// No Op'))
      ->addMethod(javaMethod('public', javaVoid(), 'destroyReading', [], '// No Op'))
      ->addIsFinished($itr)
      ->addReadCheckedObjects($data, $path, $context, $itr);
}

function <<access.private>> meta::alloy::runtime::java::graphFetch::stream::addConstructor(class:meta::java::metamodel::Class[1], sourceClass:meta::java::metamodel::Class[1]): meta::java::metamodel::Class[1]
{
   let in    = j_parameter(javaStream($sourceClass),'in');
   let jThis = j_this($class);

   $class->addConstructor(javaConstructor([], [$in]->cast(@meta::java::metamodel::Parameter),
      [
         $jThis->j_field('stream')->j_assign($in),
         $jThis->j_field('itr')->j_assign($jThis->j_field('stream')->j_invoke('iterator', []))
      ]->codesToString($class)
   ));
}

function <<access.private>> meta::alloy::runtime::java::graphFetch::stream::addIsFinished(class:meta::java::metamodel::Class[1], itr:meta::java::metamodel::Field[1]): meta::java::metamodel::Class[1]
{
   $class->addMethod(javaMethod(['public'], javaBoolean(), 'isFinished', [],
      [
         j_not(j_this($class)->j_field($itr)->j_invoke('hasNext', []))->j_return()
      ]->codesToString($class)
   ));
}

function <<access.private>> meta::alloy::runtime::java::graphFetch::stream::addReadCheckedObjects(class:meta::java::metamodel::Class[1], data:GraphFetchModelToModelData[1], path:String[1], context:GenerationContext[1], itr:meta::java::metamodel::Field[1]): meta::java::metamodel::Class[1]
{
   let checkedClass  = $context.baseProject->toOne()->resolve($context.conventions->className(Checked));
   let checkedSource = javaParameterizedType($checkedClass, $data.sourceClass);

   let object        = j_variable($data.sourceClass, 'object');
   let checked       = j_variable($checkedSource, 'checked');
   let iter          = j_this($class)->j_field($itr);

   $class->addMethod(javaMethod('public', javaCollection($checkedSource), 'readCheckedObjects', [],
      [
         j_if($iter->j_invoke('hasNext', []),
            [
               $object->j_declare($iter->j_invoke('next', [])),
               $checked->j_declare($context.conventions->dynamicChecked(
                  javaCollections()->j_invoke('emptyList', [], javaCollection($checkedSource)),
                  j_null(),
                  $object
               )),
               javaCollections()->j_invoke('singleton', $checked, javaCollection($checkedSource))->j_return()
            ]->j_block(),
            javaCollections()->j_invoke('emptyList', [], javaCollection($checkedSource))->j_return()
        )
      ]->codesToString($class)
   ));
}

