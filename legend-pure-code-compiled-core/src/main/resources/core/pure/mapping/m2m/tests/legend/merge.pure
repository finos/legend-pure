// Copyright 2021 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::executionPlan::profiles::*;
import meta::pure::mapping::modelToModel::*;
import meta::pure::runtime::*;
import meta::json::*;
import meta::pure::mapping::modelToModel::test::alloy::utils::*;
import meta::pure::graphFetch::execution::*;
Class meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithFirstName
{
   sourceFirstName:String[0..1];
   sourceAddress: meta::pure::mapping::modelToModel::test::alloy::merge::SourceAddress[0..1];
}

Class meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithLastName
{
   sourceLastName:String[0..1];
   sourceAddress: meta::pure::mapping::modelToModel::test::alloy::merge::SourceAddressWithPostCode[0..1];

}

Class meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithAge
{
   sourceAge:Integer[0..1];
}


Class meta::pure::mapping::modelToModel::test::alloy::merge::SourceAddress
{
   sourceStreet:String[0..1];
}

Class meta::pure::mapping::modelToModel::test::alloy::merge::SourceAddressWithPostCode
{
   sourcePostCode:String[0..1];
}



Class meta::pure::mapping::modelToModel::test::alloy::merge::Address
{
   street:String[0..1];
   postCode:String[0..1];
}

Class meta::pure::mapping::modelToModel::test::alloy::merge::Firm
{
   ceo : meta::pure::mapping::modelToModel::test::alloy::merge::Person[1];
   address: meta::pure::mapping::modelToModel::test::alloy::merge::Address[0..1];
}


Class meta::pure::mapping::modelToModel::test::alloy::merge::PersonCombined
{
   fullName : String[0..1];
}


Class meta::pure::mapping::modelToModel::test::alloy::merge::Person
{
   firstName : String[0..1];
   lastName : String[0..1];
   age:Integer[0..1]; //un-mapped property
   address:meta::pure::mapping::modelToModel::test::alloy::merge::Address[0..1]; 
}


function <<meta::pure::profiles::test.Test, meta::pure::profiles::test.AlloyOnly>> {serverVersion.start='v1_20_0'}
meta::pure::mapping::modelToModel::test::alloy::merge::testSimplemerge():Any[*]
{
   let tree = #{meta::pure::mapping::modelToModel::test::alloy::merge::Person {firstName,lastName,address{street},age}}#;

   let result = execute(
      |meta::pure::mapping::modelToModel::test::alloy::merge::Person.all()->graphFetch($tree)->serialize($tree),
      meta::pure::mapping::modelToModel::test::alloy::merge::SimpleMergeModelMapping,
         ^Runtime(
               connections =[ ^JsonModelConnection(
                  element=^ModelStore(),
                  class = meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithFirstName,
                  url = 'data:application/json,{"sourceFirstName":"John","sourceAddress":{"sourceStreet":"Main st"}}'
                                 ),
                        ^JsonModelConnection(
                     element=^ModelStore(),
                     class = meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithLastName,
                     url = 'data:application/json,{"sourceLastName":"Smith"}'
                  ),
                 ^JsonModelConnection(
                     element=^ModelStore(),
                     class = meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithAge,
                     url = 'data:application/json,{"sourceAge":10}'
                  )

   ]
               ),
      
      []
   );

   let json = $result.values->toOne();
   let expected = '{"firstName":"John","lastName":"Smith","address":{"street":"Main st"},"age":10}';
   assert(jsonEquivalent($expected->parseJSON(), $json->parseJSON()));
}
function <<meta::pure::profiles::test.Test, meta::pure::profiles::test.AlloyOnly>> {serverVersion.start='v1_20_0'}
meta::pure::mapping::modelToModel::test::alloy::merge::testMergeNonRoot():Any[*]
{
   let tree = #{meta::pure::mapping::modelToModel::test::alloy::merge::Firm {ceo{firstName,lastName,address{street,postCode}}}}#; 

   let result = execute(
      |meta::pure::mapping::modelToModel::test::alloy::merge::Firm.all()->graphFetch($tree)->serialize($tree),
      meta::pure::mapping::modelToModel::test::alloy::merge::SimpleMergeModelMappingNonRoot,
         ^Runtime(
               connections =[ ^JsonModelConnection(
                  element=^ModelStore(),
                  class = meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithFirstName,
                  url = 'data:application/json,{"sourceFirstName":"John","sourceAddress":{"sourceStreet":"Main st"}}'
                                 ),
                        ^JsonModelConnection(
                     element=^ModelStore(),
                     class = meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithLastName,
                     url = 'data:application/json,{"sourceLastName":"Smith","sourceAddress":{"sourcePostCode":"ABCD"}}'
                  ),
                 ^JsonModelConnection(
                     element=^ModelStore(),
                     class = meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithAge,
                     url = 'data:application/json,{"sourceAge":10}'
                  ),
                  
                   ^JsonModelConnection(
                     element=^ModelStore(),
                     class = meta::pure::mapping::modelToModel::test::alloy::merge::SourceAddress,
                     url = 'data:application/json,:{"sourceStreet":"Elm st"}'
                  )

   ]
               ),
      
      []
   );

   let json = $result.values->toOne();
   let expected = '{"ceo":{"firstName":"John","lastName":"Smith","address":{"street":"Main st","postCode":"ABCD"}}}';
   assert(jsonEquivalent($expected->parseJSON(), $json->parseJSON()));
}


function <<meta::pure::profiles::test.Test, meta::pure::profiles::test.AlloyOnly>> { serverVersion.start='v1_20_0'}
meta::pure::mapping::modelToModel::test::alloy::merge::mergeObjectsModelChainMapping():Any[*]
{
   let tree = #{meta::pure::mapping::modelToModel::test::alloy::merge::PersonCombined {fullName}}#;

   let result = execute(
      |meta::pure::mapping::modelToModel::test::alloy::merge::PersonCombined.all()->graphFetch($tree)->serialize($tree),
      meta::pure::mapping::modelToModel::test::alloy::merge::MergeObjectsModelMappingChain,
         ^Runtime(
               connections =[ 
               ^ModelChainConnection(element = ^ModelStore(), mappings = [meta::pure::mapping::modelToModel::test::alloy::merge::SimpleMergeModelMappingNonRoot]),

                   ^JsonModelConnection(
                  element=^ModelStore(),
                  class = meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithFirstName,
                  url = 'data:application/json,{"sourceFirstName":"John","sourceAddress":{"sourceStreet":"Main st"}}'
                                 ),
                        ^JsonModelConnection(
                     element=^ModelStore(),
                     class = meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithLastName,
                     url = 'data:application/json,{"sourceLastName":"Smith","sourceAddress":{"sourcePostCode":"ABCD"}}'
                  ),
                 ^JsonModelConnection(
                     element=^ModelStore(),
                     class = meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithAge,
                     url = 'data:application/json,{"sourceAge":10}'
                  ),
                  
                   ^JsonModelConnection(
                     element=^ModelStore(),
                     class = meta::pure::mapping::modelToModel::test::alloy::merge::SourceAddress,
                     url = 'data:application/json,:{"sourceStreet":"Elm st"}'
                  )
      ]
      
   ),[]);

   let json = $result.values->toOne();
   let expected = '{"fullName":"John Smith"}';
   assert(jsonEquivalent($expected->parseJSON(), $json->parseJSON()));
}


###Mapping
Mapping meta::pure::mapping::modelToModel::test::alloy::merge::SimpleMergeModelMapping
(
   *meta::pure::mapping::modelToModel::test::alloy::merge::Person : Operation
           {
              meta::pure::router::operations::merge_OperationSetImplementation_1__SetImplementation_MANY_(p1,p2,p3)
           }

   meta::pure::mapping::modelToModel::test::alloy::merge::Person[p1] : Pure
            {
               ~src 	meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithFirstName
               firstName : $src.sourceFirstName,
               address : $src.sourceAddress
            }

   meta::pure::mapping::modelToModel::test::alloy::merge::Person[p2] : Pure
            {
               ~src 	meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithLastName
               lastName :  $src.sourceLastName
            }
   
      meta::pure::mapping::modelToModel::test::alloy::merge::Person[p3] : Pure
            {
               ~src 	meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithAge
               age :  $src.sourceAge

            }
       meta::pure::mapping::modelToModel::test::alloy::merge::Address : Pure
            {
               ~src 	meta::pure::mapping::modelToModel::test::alloy::merge::SourceAddress
               street :  $src.sourceStreet
            }
   
)



###Mapping
Mapping meta::pure::mapping::modelToModel::test::alloy::merge::SimpleMergeModelMappingNonRoot
(
   *meta::pure::mapping::modelToModel::test::alloy::merge::Firm : Operation
           {
              meta::pure::router::operations::merge_OperationSetImplementation_1__SetImplementation_MANY_(f1,f2)
           }
   *meta::pure::mapping::modelToModel::test::alloy::merge::Person : Operation
           {
              meta::pure::router::operations::merge_OperationSetImplementation_1__SetImplementation_MANY_(p1,p2)
           }
   
              
   *meta::pure::mapping::modelToModel::test::alloy::merge::Address : Operation
           {
              meta::pure::router::operations::merge_OperationSetImplementation_1__SetImplementation_MANY_(a1,a2)
           }
   

    meta::pure::mapping::modelToModel::test::alloy::merge::Firm[f1] : Pure
   {
      ~src meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithFirstName
       ceo[p1]:$src
      
   }  
   meta::pure::mapping::modelToModel::test::alloy::merge::Person[p1] : Pure
            {
               ~src 	meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithFirstName
               firstName : $src.sourceFirstName,
               address[a1] : $src.sourceAddress
            }

  
       meta::pure::mapping::modelToModel::test::alloy::merge::Address[a1] : Pure
            {
               ~src 	meta::pure::mapping::modelToModel::test::alloy::merge::SourceAddress
               street :  $src.sourceStreet
            }

      meta::pure::mapping::modelToModel::test::alloy::merge::Firm[f2] : Pure
   {
      ~src meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithLastName
       ceo[p2]:$src
      
   }
    meta::pure::mapping::modelToModel::test::alloy::merge::Person[p2] : Pure
            {
               ~src 	meta::pure::mapping::modelToModel::test::alloy::merge::SourcePersonWithLastName
               lastName :  $src.sourceLastName,
               address[a2] : $src.sourceAddress

            }

     meta::pure::mapping::modelToModel::test::alloy::merge::Address[a2] : Pure
            {
               ~src 	meta::pure::mapping::modelToModel::test::alloy::merge::SourceAddressWithPostCode
               postCode :  $src.sourcePostCode
            }
)


###Mapping
Mapping meta::pure::mapping::modelToModel::test::alloy::merge::MergeObjectsModelMappingChain
(
   meta::pure::mapping::modelToModel::test::alloy::merge::PersonCombined : Pure
            {
               ~src 	meta::pure::mapping::modelToModel::test::alloy::merge::Person
               fullName : $src.firstName->toOne() + ' ' + $src.lastName->toOne()
            }

)

