// Copyright 2021 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::alloy::runtime::java::*;
import meta::alloy::runtime::java::naming::*;
import meta::alloy::runtime::java::typeInfo::*;
import meta::external::format::xml::executionPlan::*;
import meta::external::format::xml::runtime::*;
import meta::external::shared::*;
import meta::external::shared::executionPlan::*;
import meta::external::shared::runtime::*;
import meta::java::generation::code::*;
import meta::java::generation::convention::*;
import meta::java::generation::utils::*;
import meta::java::metamodel::factory::*;
import meta::java::metamodel::project::*;
import meta::protocols::pure::vX_X_X::transformation::fromPureGraph::external::format::xml::*;
import meta::pure::executionPlan::*;
import meta::pure::executionPlan::engine::*;
import meta::pure::executionPlan::toString::*;
import meta::pure::graphFetch::*;
import meta::pure::router::extension::*;

Class meta::external::format::xml::executionPlan::XmlSerializeExecutionNode extends ExecutionNode
{
}

Class meta::external::format::xml::executionPlan::XmlDeserializeExecutionNode extends ExecutionNode
{
   connection : UrlConnection[1];
   tree       : RootGraphFetchTree<Any>[0..1]; 
}

function meta::external::format::xml::executionPlan::generateSerializeNode(fe:FunctionExpression[1], children:ExecutionNode[*]): ExecutionNode[1]
{
   ^XmlSerializeExecutionNode(
      resultType      = ^ResultType(type=String),
      resultSizeRange = PureOne,
      executionNodes  = $children
   );
}

function meta::external::format::xml::executionPlan::generateDeserializeNode(connection:UrlConnection[1], tree:RootGraphFetchTree<Any>[0..1]): ExecutionNode[1]
{
   ^XmlDeserializeExecutionNode(
      resultType      = ^PartialClassResultType(
                           type=$connection.class,
                           propertiesWithParameters = $tree.subTrees->cast(@PropertyGraphFetchTree)->map(x | $x->map(x | ^PropertyWithParameters(property = $x.property, parameters = $x.parameters)))
                        ),
      resultSizeRange = ZeroMany,
      connection      = $connection,
      tree            = $tree
   );
}

function meta::external::format::xml::executionPlan::printPlanNodeToString(space:String[1], extensions:RouterExtension[*]): Function<{Nil[1]->String[1]}>[*]
{
   [
      {node:XmlSerializeExecutionNode[1] |
         'XML_Serialize\n' + 
         $space + '(' + header($node, $space, $extensions) + '\n' +
         $node->childrenToString($space+'  ', $extensions) + '\n' +
         $node.implementation->printImplementation('implementation', $space+'  ', $extensions) +
         $space + ')\n'
      },
      {node:XmlDeserializeExecutionNode[1] |
         'XML_Deserialize\n' + 
         $space + '(' + header($node, $space, $extensions) + '\n' +
         $space + '  connection = ' + $node.connection->meta::pure::executionPlan::toString::connectionToString($extensions) + '\n' +
         $node->childrenToString($space+'  ', $extensions) + '\n' +
         $node.implementation->printImplementation('implementation', $space+'  ', $extensions) +
         $space + ')\n'
      }
   ]
}

function meta::external::format::xml::executionPlan::planJavaPrepare(path:String[1], context:GenerationContext[1], extensions:RouterExtension[*], debug:DebugContext[1]): Function<{Nil[1]->GenerationContext[1]}>[*]
{
   [
      node:XmlSerializeExecutionNode[1]   | prepareForSerialize($node, $path, $context, $extensions, $debug),
      node:XmlDeserializeExecutionNode[1] | prepareForDeserialize($node, $path, $context, $extensions, $debug)
   ]
}

function meta::external::format::xml::executionPlan::planJavaGenerate(path:String[1], context:GenerationContext[1], extensions:RouterExtension[*], debug:DebugContext[1]): Function<{Nil[1]->GeneratedCode[1]}>[*]
{
   [
       node:XmlSerializeExecutionNode[1]   | generateForSerialize($node, $path, $context, $extensions, $debug),
       node:XmlDeserializeExecutionNode[1] | generateForDeserialize($node, $path, $context, $extensions, $debug)
   ]
}

function <<access.private>> meta::external::format::xml::executionPlan::prepareForSerialize(node:XmlSerializeExecutionNode[1], path:String[1], context:GenerationContext[1], extensions:RouterExtension[*], debug:DebugContext[1]): GenerationContext[1]
{
   print(if($debug.debug,|$debug.space+'('+$path+') prepare XmlSerializeExecutionNode\n', |''));

   $context;
}

function <<access.private>> meta::external::format::xml::executionPlan::prepareForDeserialize(node:XmlDeserializeExecutionNode[1], path:String[1], context:GenerationContext[1], extensions:RouterExtension[*], debug:DebugContext[1]): GenerationContext[1]
{
   print(if($debug.debug,|$debug.space+'('+$path+') prepare XmlDeserializeExecutionNode\n', |''));

   let conventions = $context.conventions;
   let commons     = ^DeserializeCommons();

   let class       = $node.connection.class;
   let simpleType  = ^SimpleJavaType(pureType=$class, javaType=$conventions->className($class));
   let returnType  = ^CheckedJavaType(checkedOf=$simpleType, source=^SimpleJavaType(pureType=XmlDataRecord, javaType=$conventions->className(XmlDataRecord)));
   
   assert($node.tree->isNotEmpty(), |'Use case without graph fetch tree not yet supported');
   let newTypeInfos = $context.typeInfos
                         ->addForGraphFetchTree($node.tree->toOne())
                         ->addForClassWithAllProperties(XmlDataRecord);

   $context
      ->setConventions($conventions->addProvidedType(XmlDataRecord, $commons.xmlDataRecord()))
      ->setTypeInfos($newTypeInfos)
      ->addNodeInfo(^NodeInfo(path=$path, returnType=$returnType, graphFetchTrees=#{XmlDataRecord {number, record}}#));
}

function <<access.private>> meta::external::format::xml::executionPlan::generateForDeserialize(node:XmlDeserializeExecutionNode[1], path:String[1], context:GenerationContext[1], extensions:RouterExtension[*], debug:DebugContext[1]): GeneratedCode[1]
{
   print(if($debug.debug,|$debug.space+'('+$path+') generate XmlDeserializeExecutionNode\n', |''));

   let stream         = j_parameter(javaInputStream(), 'stream');
   let rootClass      = $node.connection.class;

   let readableClasses = $rootClass->concatenate($context.typeInfos->classDependenciesViaPropertiesWithsubTypes($rootClass));
   let project         = $readableClasses->createDataClassesProject($path, $context, $debug->indent());
   let readerClass     = createReaderClass($node, $path, $context);

   $project->addClasses($readerClass)->generatedCode($readerClass);
}

function <<access.private>> meta::external::format::xml::executionPlan::createReaderClass(node:XmlDeserializeExecutionNode[1], path:String[1], context:GenerationContext[1]): meta::java::metamodel::Class[1]
{   
   let commons    = ^DeserializeCommons();
   let rootClass = $node.connection.class;   
   let ctxParam  = j_parameter(javaParameterizedType($commons.deserializeContext, javaWildcard()), 'context');

   $context.conventions->readerClass($path, $rootClass)
      ->implements($commons.iNodeSpecifics)
      ->addReadNoSchemaMethod($ctxParam, $node, $path, $context);
}

function <<access.private>> meta::external::format::xml::executionPlan::addReadNoSchemaMethod(jc:meta::java::metamodel::Class[1], ctxParam:Code[1], node:XmlDeserializeExecutionNode[1], path:String[1], context:GenerationContext[1]): meta::java::metamodel::Class[1]
{   
   let commons   = ^DeserializeCommons();
   let rootClass = $node.connection.class;   

   $jc->addMethod(cls|
           javaMethod('public', javaVoid(), 'read', $ctxParam,
              j_new(
                 $commons.hDocument, 
                 j_new(
                    $commons.hAnySurroundingElements, 
                    j_this($jc)->j_invoke($context.conventions->elementMethodName($rootClass), [$ctxParam, j_long(1), j_long(1), j_null()], $commons.hElement)
                 )
              )->j_invoke('process', $ctxParam, javaVoid())
           )
        )
        ->addMethods(c| $c->createClassElementNoSchemaMethods($ctxParam, $rootClass, [], $path, $context));
}

Class <<access.private>> meta::external::format::xml::executionPlan::PropertyCodes
{
   elementVar    : Code[1];
   elementInit   : Code[1];
   attributeVar  : Code[0..1];
   attributeInit : Code[0..1];
   calledMethods : meta::java::metamodel::Method[*];
   
   elementDeclare()   { $this.elementVar->j_declare($this.elementInit); }: Code[1];
   attributeDeclare() { $this.attributeVar->map(v| $v->j_declare($this.attributeInit->toOne())); }: Code[0..1];
}

function <<access.private>> meta::external::format::xml::executionPlan::createClassElementNoSchemaMethods(jc:meta::java::metamodel::Class[1], ctxParam:Code[1], class:Class<Any>[1], property:Property<Nil,Any|*>[0..1], path:String[1], context:GenerationContext[1]): meta::java::metamodel::Method[*]
{   
   let conventions = $context.conventions;
   let commons     = ^DeserializeCommons();
   
   let reverseProp = if($property->isNotEmpty() && $property.owner->toOne()->instanceOf(Association),
                        | $property.owner->cast(@Association).properties->filter(p| $p != $property)->toOne(),
                        | []
                     );
   
   let dataClass   = $conventions->dataClass($class, $path);
   let factory     = $dataClass->j_field($commons.dataFactoryFieldName, $commons.iExternalDataFactory);
   
   let propertyCodes = $context.typeInfos->allProperties($class)
      ->filter(p| $p != $reverseProp)
      ->map({p|
         let propType  = $p->functionReturnType().rawType->toOne();
         if($propType->instanceOf(Class),
            {|
               let propClass  = $propType->cast(@Class<Any>);
               let getAdder   = $dataClass->j_invoke($commons.adderGetterName, j_string($p.name->toOne()), $commons.adder)->j_cast($commons.objectAdder);
               let propOccurs = occursFromMultiplicity($p.multiplicity);
               let elementVar = j_variable($commons.hElement, $conventions->identifier($p.name->toOne() + 'Element'));
            
               ^PropertyCodes(
                  elementVar    = $elementVar,
                  elementInit   = j_this($jc)->j_invoke($context.conventions->elementMethodName($propClass), [$ctxParam, $propOccurs.first, $propOccurs.second, $getAdder], $commons.hElement),
                  calledMethods = createClassElementNoSchemaMethods($jc, $ctxParam, $propClass, $p, $path, $context)
               );
            },
            | $p->codePrimitivePropertyNoSchema($dataClass, $ctxParam, $context)
         );
   });                                                                       
                                                                          
   let minOccursParam = j_parameter(javaLong(), 'minOccurs');
   let maxOccursParam = j_parameter(javaLong(), 'maxOccurs');
   let adderParam     = j_parameter($commons.objectAdder, 'adder');

   let classElementVar     = j_variable($commons.hElement, '_element');
   let classElementName    = j_conditional($adderParam->j_eq(j_null()),
                                j_string($class.name->toOne()), 
                                $adderParam->j_invoke('getPropertyName', [], javaString())
                             );
   let classElementParams  = [$minOccursParam, $maxOccursParam, $classElementName, $factory, $adderParam];
   let classElementDeclare = $classElementVar->j_declare(
                                j_conditional($maxOccursParam->j_le(j_long(1)),
                                   $commons.hElement->j_invoke('ofLenient', $classElementParams, $commons.hElement),
                                   $commons.hFlexCollectionElement->j_invoke('ofLenient', $classElementParams, $commons.hElement)
                                )
                             );
   let withAttrsAdded      = $propertyCodes.attributeVar->fold({v, c| $c->j_invoke('add', $v, $commons.hElement)}, $classElementVar);

   let elementVars         = $propertyCodes.elementVar;
   let choiceVar           = j_variable($commons.hChoice, '_choice');
   let choiceOf            = $commons.hChoice->j_invoke('of', [j_long(0), javaLongMax()], $commons.hChoice);
   let choiceWithElements  = $elementVars->fold({v, c| $c->j_invoke('add', $v, $commons.hChoice)}, $choiceOf);
   let choiceDeclare       = if($elementVars->isEmpty(),
                                | [],
                                | $choiceVar->j_declare($choiceWithElements)
                             );
   let withChoiceAdded     = if($elementVars->isEmpty(),
                                | $withAttrsAdded,
                                | $withAttrsAdded->j_invoke('add', $choiceVar, $commons.hElement)
                             );

   javaMethod('private', $commons.hElement, $conventions->elementMethodName($class), [$ctxParam, $minOccursParam, $maxOccursParam, $adderParam],
      $propertyCodes.attributeDeclare
         ->concatenate($propertyCodes.elementDeclare)
         ->concatenate($choiceDeclare)
         ->concatenate($classElementDeclare)
         ->concatenate(j_return($withChoiceAdded))
   )->concatenate($propertyCodes.calledMethods)->removeDuplicatesBy(m| $m.name);
}

function <<access.private>> meta::external::format::xml::executionPlan::codePrimitivePropertyNoSchema(prop:Property<Nil,Any|*>[1], dataClass:meta::java::metamodel::Class[1], ctxParam:Code[1], context:GenerationContext[1]): PropertyCodes[1]
{
   let conventions = $context.conventions;
   let commons     = ^DeserializeCommons();
   let pureDate    = $conventions->className(PureDate);

   let attributeVar = j_variable($commons.hAttribute, $conventions->identifier($prop.name->toOne() + 'Attribute'));
   let elementVar   = j_variable($commons.hElement, $conventions->identifier($prop.name->toOne() + 'Element'));
   let pOccurs      = occursFromMultiplicity($prop.multiplicity);

   let propType  = $prop->functionReturnType().rawType->toOne();
   let javaType  = $conventions->pureTypeToJavaType($prop);
   let itemType  = if($javaType->isJavaList(), |$javaType->elementType(), |$javaType)->toUnboxed();
      
   let getAdder  = $dataClass->j_invoke($commons.adderGetterName, j_string($prop.name->toOne()), $commons.adder);
   let addTo     = if($propType->instanceOf(Enumeration),
                      {|
                         let addToType         = javaParameterizedType($commons.addEnumToObject, [$dataClass, $itemType]);
                         let simpleTypeHandler = $ctxParam->j_invoke('stringSimpleTypeHandler', [], javaParameterizedType($commons.simpleTypeHandler, javaString()));
                         j_new($addToType, [$getAdder, $simpleTypeHandler, $itemType->j_field('class')]);
                      },
                      |
                   if($itemType == javaBoolean(),
                      {|
                         let addToType         = javaParameterizedType($commons.addBooleanToObject, $dataClass);
                         let simpleTypeHandler = $ctxParam->j_invoke('booleanSimpleTypeHandler', [], $commons.booleanSimpleTypeHandler);
                         j_new($addToType, [$getAdder, $simpleTypeHandler]);
                      },
                      |
                   if($itemType == javaLong(),
                      {|
                         let addToType         = javaParameterizedType($commons.addLongToObject, $dataClass);
                         let simpleTypeHandler = $ctxParam->j_invoke('longSimpleTypeHandler', [], $commons.longSimpleTypeHandler);
                         j_new($addToType, [$getAdder, $simpleTypeHandler]);
                      },
                      |
                   if($itemType == javaDouble(),
                      {|
                         let addToType         = javaParameterizedType($commons.addDoubleToObject, $dataClass);
                         let simpleTypeHandler = $ctxParam->j_invoke('doubleSimpleTypeHandler', [], $commons.doubleSimpleTypeHandler);
                         j_new($addToType, [$getAdder, $simpleTypeHandler]);
                      },
                      |
                   if($itemType == javaString(),
                      {|
                         let addToType         = javaParameterizedType($commons.addObjectToObject, [$dataClass, javaString()]);
                         let simpleTypeHandler = $ctxParam->j_invoke('stringSimpleTypeHandler', [], javaParameterizedType($commons.simpleTypeHandler, javaString()));
                         j_new($addToType, [$getAdder, $simpleTypeHandler]);
                      },
                      |
                   if($itemType == $pureDate, 
                      {|
                         let addToType         = javaParameterizedType($commons.addObjectToObject, [$dataClass, javaTemporal()]);
                         let simpleTypeHandler = $ctxParam->j_invoke(if($propType == StrictDate, |'dateSimpleTypeHandler', |'datetimeSimpleTypeHandler'), [], javaParameterizedType($commons.simpleTypeHandler, javaTemporal()));
                         j_new($addToType, [$getAdder, $simpleTypeHandler]);
                      },
                      |
                   if($itemType == javaBigDecimal(), 
                      {|
                         let addToType         = javaParameterizedType($commons.addObjectToObject, [$dataClass, javaBigDecimal()]);
                         let simpleTypeHandler = $ctxParam->j_invoke('decimalSimpleTypeHandler', [], javaParameterizedType($commons.simpleTypeHandler, javaBigDecimal()));
                         j_new($addToType, [$getAdder, $simpleTypeHandler]);
                      },
                      {|
                         fail('Unsupported type ' + $itemType->meta::java::serialization::typePathToString(true));
                         j_null();
                      }
                   )))))));
      
   let textContentType  = javaParameterizedType($commons.hTextContent, $dataClass);
   let textContent      = j_new($textContentType, $addTo); 
   let attributeAllowed = $prop.multiplicity == ZeroOne || $prop.multiplicity == PureOne;
      
   ^PropertyCodes(
      elementVar    = $elementVar,
      elementInit   = $commons.hElement->j_invoke('ofLenient', [$pOccurs.first, $pOccurs.second, j_string($prop.name->toOne())], $commons.hElement)->j_invoke('add', $textContent, $commons.hElement),
      attributeVar  = if($attributeAllowed, |$attributeVar, |[]),
      attributeInit = if($attributeAllowed, |$commons.hAttribute->j_invoke('ofLenient', [j_long(0), $pOccurs.second, j_string($prop.name->toOne()), $addTo], $commons.hAttribute), |[])
   );
}

function <<access.private>> meta::external::format::xml::executionPlan::elementMethodName(conventions:Conventions[1], class:Class<Any>[1]): String[1]
{   
   'element_' + $conventions->identifier($class->elementToPath('_'));
}

function meta::external::format::xml::executionPlan::readerClass(conventions:Conventions[1], path:String[1], cls:Class<Any>[1]): meta::java::metamodel::Class[1]
{   
   $conventions->planNodeClass('public', $path, 'XmlReader_' + $conventions->identifier($cls->elementToPath('_')));
}


function <<access.private>> meta::external::format::xml::executionPlan::generateForSerialize(node:XmlSerializeExecutionNode[1], path:String[1], context:GenerationContext[1], extensions:RouterExtension[*], debug:DebugContext[1]): GeneratedCode[1]
{
   print(if($debug.debug,|$debug.space+'('+$path+') generate XmlSerializeExecutionNode\n', |''));
   // TODO Serialization
   ^GeneratedCode();
}

Class <<access.private>> meta::external::format::xml::executionPlan::DeserializeCommons extends meta::external::shared::executionPlan::ExternalSharedCommons
{
   iNodeSpecifics()           { javaClass('org.finos.legend.engine.external.format.xml.read.IXmlDeserializeExecutionNodeSpecifics') }: meta::java::metamodel::Class[1];
   deserializeContext()       { javaClass('org.finos.legend.engine.external.format.xml.read.DeserializeContext')                    }: meta::java::metamodel::Class[1];
   
   xmlDataRecord()            { javaClass('org.finos.legend.engine.external.format.xml.read.XmlDataRecord')                         }: meta::java::metamodel::Class[1];
   
   addBooleanToObject()       { javaClass('org.finos.legend.engine.external.format.xml.read.valueProcessors.AddBooleanToObject')    }: meta::java::metamodel::Class[1];
   addDoubleToObject()        { javaClass('org.finos.legend.engine.external.format.xml.read.valueProcessors.AddDoubleToObject')     }: meta::java::metamodel::Class[1];
   addLongToObject()          { javaClass('org.finos.legend.engine.external.format.xml.read.valueProcessors.AddLongToObject')       }: meta::java::metamodel::Class[1];
   addObjectToObject()        { javaClass('org.finos.legend.engine.external.format.xml.read.valueProcessors.AddObjectToObject')     }: meta::java::metamodel::Class[1];
   addEnumToObject()          { javaClass('org.finos.legend.engine.external.format.xml.read.valueProcessors.AddEnumToObject')       }: meta::java::metamodel::Class[1];
   
   simpleTypeHandler()        { javaClass('org.finos.legend.engine.external.format.xml.shared.datatypes.SimpleTypeHandler')         }: meta::java::metamodel::Class[1];
   booleanSimpleTypeHandler() { javaClass('org.finos.legend.engine.external.format.xml.shared.datatypes.BooleanSimpleTypeHandler')  }: meta::java::metamodel::Class[1];
   longSimpleTypeHandler()    { javaClass('org.finos.legend.engine.external.format.xml.shared.datatypes.LongSimpleTypeHandler')     }: meta::java::metamodel::Class[1];
   doubleSimpleTypeHandler()  { javaClass('org.finos.legend.engine.external.format.xml.shared.datatypes.DoubleSimpleTypeHandler')   }: meta::java::metamodel::Class[1];
    
   hAll()                     { javaClass('org.finos.legend.engine.external.format.xml.read.handlers.All')                          }: meta::java::metamodel::Class[1];
   hAnySurroundingElements()  { javaClass('org.finos.legend.engine.external.format.xml.read.handlers.AnySurroundingElements')       }: meta::java::metamodel::Class[1];
   hAttribute()               { javaClass('org.finos.legend.engine.external.format.xml.read.handlers.Attribute')                    }: meta::java::metamodel::Class[1];
   hChoice()                  { javaClass('org.finos.legend.engine.external.format.xml.read.handlers.Choice')                       }: meta::java::metamodel::Class[1];
   hDocument()                { javaClass('org.finos.legend.engine.external.format.xml.read.handlers.Document')                     }: meta::java::metamodel::Class[1];
   hElement()                 { javaClass('org.finos.legend.engine.external.format.xml.read.handlers.Element')                      }: meta::java::metamodel::Class[1];
   hFlexCollectionElement()   { javaClass('org.finos.legend.engine.external.format.xml.read.handlers.FlexCollectionElement')        }: meta::java::metamodel::Class[1];
   hSequence()                { javaClass('org.finos.legend.engine.external.format.xml.read.handlers.Sequence')                     }: meta::java::metamodel::Class[1];
   hTextContent()             { javaClass('org.finos.legend.engine.external.format.xml.read.handlers.TextContent')                  }: meta::java::metamodel::Class[1];
}
