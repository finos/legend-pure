// Copyright 2023 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::metamodel::relation::*;

native function meta::pure::functions::relation::extend<T,Z>(r:Relation<T>[1], f:FuncColSpec<{T[1]->Any[0..1]},Z>[1]):Relation<T+Z>[1];

function meta::pure::functions::relation::extend<T,Z>(r:TDS<T>[1], f:FuncColSpec<{T[1]->Any[0..1]},Z>[1]):Relation<T+Z>[1]
{
    $r->cast(@meta::pure::metamodel::relation::Relation<T>)->meta::pure::functions::relation::extend($f);
}

native function meta::pure::functions::relation::extend<T,Z>(r:Relation<T>[1], fs:FuncColSpecArray<{T[1]->Any[0..1]},Z>[1]):Relation<T+Z>[1];

function meta::pure::functions::relation::extend<T,Z>(r:TDS<T>[1], fs:FuncColSpecArray<{T[1]->Any[0..1]},Z>[1]):Relation<T+Z>[1]
{
    $r->cast(@meta::pure::metamodel::relation::Relation<T>)->meta::pure::functions::relation::extend($fs);
}


function <<test.Test>> meta::pure::functions::relation::tests::extend::testSimpleExtendStr():Boolean[1]
{
    let res = #TDS
                value, str
                1, a
                3, ewe
                4, qw
                5, wwe
                6, weq
              #->extend(~name:c|$c.str->toOne() + $c.value->toOne()->toString());
    assertEquals(5, $res->size());
    assertEquals('a1ewe3qw4wwe5weq6', $res->map(c|$c.name)->joinStrings(''));
}

function <<test.Test>> meta::pure::functions::relation::tests::extend::testSimpleExtendInt():Boolean[1]
{
    let res = #TDS
                value, str
                1, a
                3, ewe
                4, qw
                5, wwe
                6, weq
              #->extend(~name:c|$c.value->toOne() + 1);
    assertEquals(5, $res->size());
    assertEquals('24567', $res->map(c|$c.name->toOne()->toString())->joinStrings(''));
}

function <<test.Test>> meta::pure::functions::relation::tests::extend::testSimpleExtendFloat():Boolean[1]
{
    let res = #TDS
                value, doub
                1, 1.2
                3, 2.3
                4, 4.2
                5, 4.2
                6, 4.5
              #->extend(~name:c|$c.doub->toOne() + 1.0);
    assertEquals(5, $res->size());
    assertEquals('2.23.35.25.25.5', $res->map(c|$c.name->toOne()->toString())->joinStrings(''));
}
