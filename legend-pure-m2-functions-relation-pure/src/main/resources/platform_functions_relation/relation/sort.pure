// Copyright 2023 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::metamodel::relation::*;

Enum meta::pure::functions::relation::SortType
{
    ASC, DESC
}

Class meta::pure::functions::relation::SortInfo
{
   column : String[1];
   direction : SortType[1];
}

function meta::pure::functions::relation::descending(column:String[1]):SortInfo[1]
{
   ^SortInfo(column=$column, direction=SortType.DESC)
}

function meta::pure::functions::relation::ascending(column:String[1]):SortInfo[1]
{
   ^SortInfo(column=$column, direction=SortType.ASC)
}

native function meta::pure::functions::relation::sort<T>(rel:Relation<T>[1], sortInfo:SortInfo[*]):Relation<T>[1];

function meta::pure::functions::relation::sort<T>(rel:TDS<T>[1], sortInfo:SortInfo[*]):Relation<T>[1]
{
    $rel->cast(@meta::pure::metamodel::relation::Relation<T>)->meta::pure::functions::relation::sort($sortInfo);
}


function <<test.Test>> meta::pure::functions::relation::tests::sort::testSimpleSort():Boolean[1]
{
  let tds = #TDS
              id, name
              2, George
              3, Pierre
              1, Sachin
              1, Neema
              5, David
              4, Alex
              2, Thierry
            #;

  assertEquals(
    '1 Sachin\n'+
    '1 Neema\n'+
    '2 George\n'+
    '2 Thierry\n'+
    '3 Pierre\n'+
    '4 Alex\n'+
    '5 David', $tds->sort(ascending('id'))->map(x|$x.id->s() + ' ' + $x.name->s())->joinStrings('\n'));

  assertEquals(
    '1 Neema\n'+
    '1 Sachin\n'+
    '2 George\n'+
    '2 Thierry\n'+
    '3 Pierre\n'+
    '4 Alex\n'+
    '5 David', $tds->sort([ascending('id'), ascending('name')])->map(x|$x.id->s() + ' ' + $x.name->s())->joinStrings('\n'));

  assertEquals(
    '5 David\n'+
    '4 Alex\n'+
    '3 Pierre\n'+
    '2 George\n'+
    '2 Thierry\n'+
    '1 Neema\n'+
    '1 Sachin', $tds->sort([descending('id'), ascending('name')])->map(x|$x.id->s() + ' ' + $x.name->s())->joinStrings('\n'));
}