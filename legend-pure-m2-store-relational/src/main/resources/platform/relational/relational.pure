// Copyright 2020 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

// Copyright 2020 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

import meta::pure::profiles::*;
import meta::pure::mapping::*;

import meta::relational::mapping::*;

import meta::relational::metamodel::*;
import meta::relational::metamodel::datatype::*;
import meta::relational::metamodel::execute::*;
import meta::relational::metamodel::join::*;
import meta::relational::metamodel::operation::*;
import meta::relational::metamodel::relation::*;

import meta::relational::runtime::*;

Class meta::relational::metamodel::Database extends meta::pure::store::set::SetBasedStore
{
    schemas : Schema[*];

    schema(name:String[1])
    {
        let schemas = $this.includes->map(d|$d->cast(@Database).schema($name))->concatenate($this.schemas->filter(s|$s.name == $name));
        if ($schemas->size() > 1,
                |let schema = $schemas->at(0);
                 ^$schema(tables = $schemas.tables);,
                |$schemas->first()
           );
    }:Schema[0..1];

    joins : Join[*];
    join(join:String[1])
    {
        $this.includes->map(d|$d->cast(@Database).join($join))
                      ->concatenate($this.joins->filter(j|$j.name == $join))->first();
    }:Join[0..1];

    filters : Filter[*];
    filter(filter:String[1])
    {
        $this.includes->map(d|$d->cast(@Database).filter($filter))
                      ->concatenate($this.filters->filter(j|$j.name == $filter))->first();
    }:Filter[0..1];
}

Class meta::relational::metamodel::Schema extends meta::pure::store::set::Namespace
{
    name : String[1];

    database : Database[1];

    tables : Table[*];
    table(name:String[1])
    {
        $this.tables->filter(t|$t.name == $name)->first();
    }:Table[0..1];

    views : View[*];
    view(name:String[1])
    {
        $this.views->filter(t|$t.name == $name)->first();
    }:View[0..1];
}

Class meta::relational::metamodel::relation::Relation extends meta::relational::metamodel::RelationalOperationElement, meta::pure::store::set::SetRelation
{
   columns : RelationalOperationElement[*];
}

Class meta::relational::metamodel::relation::NamedRelation extends meta::relational::metamodel::relation::Relation
{
   name : String[1];
}

Class meta::relational::metamodel::relation::Milestoning
{
    owner : Relation[0..1];
}

Class meta::relational::metamodel::relation::TemporalMilestoning extends Milestoning
{
    infinityDate : Date[0..1];
}

Class meta::relational::metamodel::relation::ProcessingMilestoning extends TemporalMilestoning
{
   in : Column[1];
   out : Column[1];
   outIsInclusive : Boolean[1];
}

Class meta::relational::metamodel::relation::BusinessMilestoning extends TemporalMilestoning
{
   from : Column[1];
   thru : Column[1];
   thruIsInclusive : Boolean[1];
}

Class meta::relational::metamodel::relation::BusinessSnapshotMilestoning extends TemporalMilestoning
{
   snapshotDate : Column[1];
}

Class meta::relational::metamodel::relation::Table extends NamedRelation
{
   schema : Schema[1];
   primaryKey : Column[*];
   milestoning : Milestoning[*];
   temporaryTable : Boolean[0..1];

   column(columnName:String[1])
   {
        $this.columns->cast(@Column)->filter(c | $c.name == $columnName )->first();
   }:Column[0..1];

   isTemporaryTable()
   {
        !$this.temporaryTable->isEmpty() && $this.temporaryTable->toOne()
   }:Boolean[1];
}

Class meta::relational::metamodel::RelationalMappingSpecification
{
    userDefinedPrimaryKey : Boolean[1];
    filter : FilterMapping[0..1];
    distinct : Boolean[0..1];
    groupBy : GroupByMapping[0..1];
    mainTableAlias : TableAlias[1];

    mainRelation(){$this.mainTableAlias.relation}:Relation[1];
    mainTable(){$this.mainTableAlias.relationalElement->match([t:Table[1]|$t, v:View[1]|$v.mainTable])}:Table[1];
}

Class meta::relational::metamodel::relation::View extends NamedRelation, RelationalMappingSpecification
{
    schema : Schema[1];
    primaryKey : Column[*];
    columnMappings : ColumnMapping[*];
}

Class meta::relational::mapping::ColumnMapping
{
    columnName : String[1];
    relationalOperationElement : RelationalOperationElement[1];
}

Class meta::relational::metamodel::join::RelationalTreeNode extends TreeNode
{
    alias : TableAlias[1];
    children()
    {
        $this.childrenData->cast(@JoinTreeNode)
    }:JoinTreeNode[*];
    childByJoinName(s:String[1])
    {
        $this.children()->filter(c| $c.join.name == $s)->first()
    }:JoinTreeNode[0..1];
}

Class meta::relational::metamodel::join::RootJoinTreeNode extends RelationalTreeNode, Relation
{
}

Class meta::relational::metamodel::join::JoinTreeNode extends RelationalTreeNode
{
   setMappingOwner : PropertyMappingsImplementation[0..1];
   database : Database[1];
   joinName : String[1];
   join : Join[1];
   joinType : JoinType[0..1];
}

Enum meta::relational::metamodel::join::JoinType
{
   INNER,
   LEFT_OUTER,
   RIGHT_OUTER
}

Class meta::relational::metamodel::Filter
{
    name : String[1];
    database : Database[0..1];
    operation : Operation[1];
}

Class meta::relational::metamodel::join::Join
{
    name : String[1];
    database : Database[0..1];
    target : TableAlias[0..1];
    aliases : Pair<TableAlias,TableAlias>[*];
    otherTableFromAlias(tableAlias:TableAlias[1])
    {
        let aliasesByTableAliasName  = $this.aliases->filter(t | ($t.first.name == $tableAlias.name));
        let aliases = if($aliasesByTableAliasName->size() > 1,|filterBySchemaName($aliasesByTableAliasName, $tableAlias.relationalElement->cast(@NamedRelation));, |$aliasesByTableAliasName);
        if ($aliases->isEmpty(), | [], | $aliases->at(0).second);
    }:TableAlias[0..1];
    otherTable(namedRelation:NamedRelation[1])
    {
        let aliasesByTableName = $this.aliases->filter(t | $t.first.relation->cast(@NamedRelation).name == $namedRelation.name);
        let aliases = if($aliasesByTableName->size() > 1,|filterBySchemaName($aliasesByTableName, $namedRelation);, |$aliasesByTableName);
        if ($aliases->isEmpty(), | [], | $aliases->at(0).second);
    }:TableAlias[0..1];
    operation : Operation[1];
}

function meta::relational::metamodel::filterBySchemaName(aliases:Pair<TableAlias, TableAlias>[*], namedRelation:NamedRelation[1]):Pair<TableAlias, TableAlias>[*]
{
    $namedRelation->match([
                        t:Table[1]|$aliases->filter(p|$p.first.relation()->instanceOf(Table) && $t->cast(@Table).schema.name == $p.first.relation()->cast(@Table).schema.name),
                        v:View[1]|$aliases->filter(p|$p.first.relation()->instanceOf(View) && $v->cast(@View).schema.name == $p.first.relation()->cast(@View).schema.name)
    ]);
}

Class meta::relational::metamodel::operation::Function extends meta::relational::metamodel::RelationalOperationElement
{
}

Class meta::relational::metamodel::operation::Operation extends meta::relational::metamodel::operation::Function
{
}

Class meta::relational::metamodel::Alias extends meta::relational::metamodel::RelationalOperationElement
{
   <<equality.Key>> name : String[1];
   <<equality.Key>> relationalElement : RelationalOperationElement[1];
}

Class meta::relational::metamodel::SQLQuery extends meta::relational::metamodel::RelationalOperationElement
{
}

Class meta::relational::metamodel::TableAlias extends Alias
{
   setMappingOwner : PropertyMappingsImplementation[0..1];

   database : Database[0..1];
   schema : String[0..1];
   relation(){$this.relationalElement->cast(@Relation)}:Relation[1];
}

Class meta::relational::metamodel::Column extends meta::relational::metamodel::RelationalOperationElement, meta::pure::store::set::SetColumn
{
   <<equality.Key>> name : String[1];
   <<equality.Key>> type : meta::relational::metamodel::datatype::DataType[1];
   nullable : Boolean[0..1];
   owner : Relation[0..1];
}

Class meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::RelationalOperationElement
{
}

Class meta::relational::metamodel::relation::SelectSQLQuery extends Relation, SQLQuery
{
   distinct : Boolean[0..1];
   data : RootJoinTreeNode[0..1];
   filteringOperation : RelationalOperationElement[*];
   groupBy : RelationalOperationElement[*];
   havingOperation : RelationalOperationElement[*];
   orderBy : OrderBy[*];
   fromRow : meta::relational::metamodel::Literal[0..1];
   toRow : meta::relational::metamodel::Literal[0..1];
   leftSideOfFilter : RelationalTreeNode[0..1];
   savedFilteringOperation : Pair<RelationalTreeNode, RelationalOperationElement>[*];
   extraFilteringOperation : RelationalOperationElement[*];
   preIsolationCurrentTreeNode : RelationalTreeNode[0..1];
}

Class meta::relational::metamodel::relation::ViewSelectSQLQuery extends Table{
   selectSQLQuery : SelectSQLQuery[1];
   view : View[1];
}

Class meta::relational::metamodel::relation::TdsSelectSqlQuery extends SelectSQLQuery, RelationalTds
{
}

Class meta::relational::metamodel::relation::TableTds extends RelationalTds
{
}

Class
{doc.doc = 'Indicates that the filter is a filter on the grain of a multi-grain table (data warehousing). The grain filter can be ignored when joining to a multi-grain table using the primary key'}
meta::relational::metamodel::MultiGrainFilter extends Filter
{
}

Class meta::relational::metamodel::operation::BinaryOperation extends Operation
{
    <<equality.Key>> left:RelationalOperationElement[1];
    <<equality.Key>> right:RelationalOperationElement[1];
}

Class meta::relational::metamodel::operation::UnaryOperation extends Operation
{
    <<equality.Key>> nested:RelationalOperationElement[1];
}

Class meta::relational::metamodel::operation::VariableArityOperation extends Operation
{
    <<equality.Key>> args:RelationalOperationElement[*];
}

Class meta::relational::metamodel::DynaFunction extends Operation
{
    <<equality.Key>> name : String[1];
    <<equality.Key>> parameters : RelationalOperationElement[*];
}


Class meta::relational::metamodel::operation::JoinStrings extends Operation
{
    strings:RelationalOperationElement[*];
    prefix:RelationalOperationElement[0..1];
    separator:RelationalOperationElement[0..1];
    suffix:RelationalOperationElement[0..1];
}

Class meta::relational::metamodel::operation::ArithmeticOperation extends Operation
{
}

Class meta::relational::metamodel::operation::VariableArithmeticOperation extends ArithmeticOperation, VariableArityOperation
{
}

Class meta::relational::metamodel::operation::SemiStructuredObjectNavigation extends Operation
{
   operand     : RelationalOperationElement[1];
   contentType : String[0..1];
   returnType  : Type[0..1];
}

Class meta::relational::metamodel::operation::SemiStructuredPropertyAccess extends SemiStructuredObjectNavigation
{
   property : RelationalOperationElement[1];
   index    : RelationalOperationElement[0..1];
}

Class meta::relational::metamodel::operation::SemiStructuredArrayElementAccess extends SemiStructuredObjectNavigation
{
   index : RelationalOperationElement[1];
}

Class meta::relational::metamodel::ColumnName extends meta::relational::metamodel::RelationalOperationElement
{
    <<equality.Key>> name : String[1];
}

Class meta::relational::metamodel::RelationalTds
{
    paths: Pair<String, PathInformation>[*];
}

Class meta::relational::metamodel::PathInformation
{
   propertyMapping : meta::pure::mapping::PropertyMapping[0..1];
   type : Type[1];
   documentation : String[0..1];
   relationalType : meta::relational::metamodel::datatype::DataType[0..1];
}

Class meta::relational::metamodel::TableAliasColumn extends meta::relational::metamodel::RelationalOperationElement
{
   setMappingOwner : PropertyMappingsImplementation[0..1];

   columnName : String[0..1];

   <<equality.Key>> alias : TableAlias[1];
   <<equality.Key>> column : Column[1];
}

Class meta::relational::metamodel::RelationalOperationElementWithJoin extends meta::relational::metamodel::RelationalOperationElement
{
    relationalOperationElement : RelationalOperationElement[0..1];
    joinTreeNode : JoinTreeNode[0..1];
}

Class meta::relational::metamodel::datatype::BigInt extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::SmallInt extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::TinyInt extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Integer extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Float extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Double extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Varchar extends meta::relational::metamodel::datatype::DataType
{
    size: Integer[1];
}

Class meta::relational::metamodel::datatype::Char extends meta::relational::metamodel::datatype::DataType
{
    size: Integer[1];
}

Class meta::relational::metamodel::datatype::Varbinary extends meta::relational::metamodel::datatype::DataType
{
    size: Integer[1];
}

Class meta::relational::metamodel::datatype::Decimal extends meta::relational::metamodel::datatype::DataType
{
    precision : Integer[1];
    scale     : Integer[1];
}

Class meta::relational::metamodel::datatype::Numeric extends meta::relational::metamodel::datatype::DataType
{
    precision : Integer[1];
    scale     : Integer[1];
}

Class meta::relational::metamodel::datatype::Timestamp extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Date extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Distinct extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Other extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Bit extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Binary extends meta::relational::metamodel::datatype::DataType
{
   size:Integer[1];
}

Class meta::relational::metamodel::datatype::Real extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::Array extends meta::relational::metamodel::datatype::DataType
{
}

Class meta::relational::metamodel::datatype::SemiStructured extends meta::relational::metamodel::datatype::DataType
{
}

function meta::relational::metamodel::datatype::dataTypeToSqlText(type: meta::relational::metamodel::datatype::DataType[1]):String[1]
{
    $type->match([
        i : meta::relational::metamodel::datatype::Integer[1] | 'INT',
        f : meta::relational::metamodel::datatype::Float[1] | 'FLOAT',
        v : meta::relational::metamodel::datatype::Varchar[1] | format('VARCHAR(%d)', $v.size),
        c : meta::relational::metamodel::datatype::Char[1] | format('CHAR(%d)', $c.size),
        d : meta::relational::metamodel::datatype::Decimal[1] | format('DECIMAL(%d, %d)', [$d.precision, $d.scale]),
        t : meta::relational::metamodel::datatype::Timestamp[1] | 'TIMESTAMP',
        d : meta::relational::metamodel::datatype::Date[1] | 'DATE',
        b : meta::relational::metamodel::datatype::BigInt[1] | 'BIGINT',
        s : meta::relational::metamodel::datatype::SmallInt[1] | 'SMALLINT',
        t : meta::relational::metamodel::datatype::TinyInt[1] | 'TINYINT',
        d : meta::relational::metamodel::datatype::Double[1] | 'DOUBLE',
        n : meta::relational::metamodel::datatype::Numeric[1] | format('NUMERIC(%d, %d)', [$n.precision, $n.scale]),
        d : meta::relational::metamodel::datatype::Distinct[1] | 'DISTINCT',
        o : meta::relational::metamodel::datatype::Other[1] | 'OTHER',
        b : meta::relational::metamodel::datatype::Bit[1] | 'BIT',
        b : meta::relational::metamodel::datatype::Binary[1] | format('BINARY(%d)', $b.size),
        r : meta::relational::metamodel::datatype::Real[1] | 'REAL',
        a : meta::relational::metamodel::datatype::Array[1] | 'ARRAY',
        v : meta::relational::metamodel::datatype::Varbinary[1] | format('VARBINARY(%d)', $v.size),
        s : meta::relational::metamodel::datatype::SemiStructured[1] | 'SEMISTRUCTURED'
    ]);
}

function meta::relational::metamodel::datatype::dataTypeToCompatiblePureType(type: meta::relational::metamodel::datatype::DataType[1]):Type[1]
{
    $type->match([
        i : meta::relational::metamodel::datatype::Integer[1] | Integer,
        f : meta::relational::metamodel::datatype::Float[1] | Float,
        v : meta::relational::metamodel::datatype::Varchar[1] | String,
        c : meta::relational::metamodel::datatype::Char[1] | String,
        d : meta::relational::metamodel::datatype::Decimal[1] | Float,
        t : meta::relational::metamodel::datatype::Timestamp[1] | DateTime,
        d : meta::relational::metamodel::datatype::Date[1] | StrictDate,
        b : meta::relational::metamodel::datatype::BigInt[1] | Integer,
        s : meta::relational::metamodel::datatype::SmallInt[1] | Integer,
        s : meta::relational::metamodel::datatype::TinyInt[1] | Integer,
        d : meta::relational::metamodel::datatype::Double[1] | Float,
        n : meta::relational::metamodel::datatype::Numeric[1] | Float,
        d : meta::relational::metamodel::datatype::Distinct[1] | String,
        o : meta::relational::metamodel::datatype::Other[1] | String,
        b : meta::relational::metamodel::datatype::Bit[1] | Boolean,
        r : meta::relational::metamodel::datatype::Real[1] | Float,
        s : meta::relational::metamodel::datatype::SemiStructured[1] | String
    ]);
}

function meta::relational::metamodel::datatype::pureTypeForDbColumn(column : meta::relational::metamodel::Column[1]):Type[1]
{
    $column.type->dataTypeToCompatiblePureType();
}

Class meta::relational::metamodel::Literal extends meta::relational::metamodel::RelationalOperationElement
{
   <<equality.Key>> value:Any[1];
}

Class meta::relational::metamodel::LiteralList extends meta::relational::metamodel::RelationalOperationElement
{
   <<equality.Key>> values:Literal[*];
}






native function meta::relational::metamodel::execute::executeInDb(sql:String[1], databaseConnection:DatabaseConnection[1], timeOutInSeconds:Integer[1], fetchSize:Integer[1]):ResultSet[1];

native function meta::relational::metamodel::execute::loadCsvToDbTable(filePath:String[1], table:Table[1], databaseConnection:DatabaseConnection[1], numberOfRows:Integer[0..1]):Nil[0];

native function meta::relational::metamodel::execute::loadValuesToDbTable(tableData:List<List<Any>>[*], table:Table[1], databaseConnection:DatabaseConnection[1]):Nil[0];

native function meta::relational::metamodel::execute::loadValuesToDbTable(tableData:List<List<Any>>[1], table:Table[1], databaseConnection:DatabaseConnection[1]):Nil[0];

native function meta::relational::metamodel::execute::fetchDbTablesMetaData(databaseConnection:DatabaseConnection[1], schemaPattern:String[0..1], tablePattern:String[0..1]):ResultSet[1];

native function meta::relational::metamodel::execute::fetchDbColumnsMetaData(databaseConnection:DatabaseConnection[1], schemaPattern:String[0..1], tablePattern:String[0..1], columnPattern:String[0..1]):ResultSet[1];

native function meta::relational::metamodel::execute::fetchDbSchemasMetaData(databaseConnection:DatabaseConnection[1], schemaPattern:String[0..1]):ResultSet[1];

native function meta::relational::metamodel::execute::fetchDbPrimaryKeysMetaData(databaseConnection:DatabaseConnection[1], schemaPattern:String[0..1], tableName:String[1]):ResultSet[1];

native function meta::relational::metamodel::execute::fetchDbImportedKeysMetaData(databaseConnection:DatabaseConnection[1], schemaPattern:String[0..1], tableName:String[1]):ResultSet[1];

native function meta::relational::metamodel::execute::createTempTable(tableName:String[1], cols:Column[*], sql:meta::pure::metamodel::function::Function<{String[1], Column[*], DatabaseType[1]->String[1]}>[1], databaseConnection:DatabaseConnection[1]):Nil[0];

native function meta::relational::metamodel::execute::createTempTable(tableName:String[1], cols:Column[*], sql:meta::pure::metamodel::function::Function<{String[1], Column[*], DatabaseType[1]->String[1]}>[1], relyOnFinallyForCleanup:Boolean[1], databaseConnection:DatabaseConnection[1]):Nil[0];

native function meta::relational::metamodel::execute::dropTempTable(tableName:String[1], databaseConnection:DatabaseConnection[1]):Nil[0];

Class meta::relational::metamodel::execute::ResultSet
{
    executionTimeInNanoSecond: Integer[1];
    connectionAcquisitionTimeInNanoSecond: Integer[1];
    executionPlanInformation : String[0..1];
    columnNames : String[*];
    rows : Row[*];

    dataSource : meta::relational::runtime::DataSource[0..1];
}

Class meta::relational::metamodel::execute::Row
{
    values : Any[*];
    parent : ResultSet[1];
    value(name:String[1]){$this.values->at($this.parent.columnNames->indexOf($name));}:Any[1];
}

Class meta::relational::metamodel::SQLNull
{
   //Used to make sure that any two SQLNull instances are equal
   <<equality.Key>> key:Nil[0];
   toString()
   {
      'SQLNull'
   }:String[1];
}

Class meta::relational::metamodel::OrderBy
{
    column : RelationalOperationElement[1];
    direction : SortDirection[1];
}

Enum meta::relational::metamodel::SortDirection
{
    ASC, DESC
}

Class meta::relational::metamodel::WindowColumn extends meta::relational::metamodel::RelationalOperationElement
{

   <<equality.Key>> columnName : String[1];

   <<equality.Key>> window: meta::relational::metamodel::Window[1];

   <<equality.Key>>  func: DynaFunction[1];

}

Class meta::relational::metamodel::Window extends meta::relational::metamodel::RelationalOperationElement
{
   <<equality.Key>> partition: RelationalOperationElement[*];

   <<equality.Key>> sortBy: RelationalOperationElement [*];

   sortDirection: meta::relational::metamodel::SortDirection[0..1] ;
}