// Copyright 2022 Goldman Sachs
//
// Licensed under the Apache License, Version 2.0 (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//      http://www.apache.org/licenses/LICENSE-2.0
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

native function meta::pure::functions::boolean::and(first:Boolean[1], second:Boolean[1]):Boolean[1];

function <<test.Test>> meta::pure::functions::boolean::tests::and::testBinaryTruthTable():Boolean[1]
{
    //Dummy commit to test if we can commit on invalid review
    assert(true && true);
    assertFalse(true && false);
    assertFalse(false && true);
    assertFalse(false && false);
}

function <<test.Test>> meta::pure::functions::boolean::tests::and::testBinaryExpressions():Boolean[1]
{
    assert((1 == 1) && (2 != 3));
    assertFalse((1 == 2) && (2 != 3));
}

function <<test.Test>> meta::pure::functions::boolean::tests::and::testTernaryTruthTable():Boolean[1]
{
    assert(true && true && true);
    assertFalse(true && true && false);
    assertFalse(true && false && true);
    assertFalse(true && false && false);
    assertFalse(false && true && true);
    assertFalse(false && true && false);
    assertFalse(false && false && true);
    assertFalse(false && false && false);
}

function <<test.Test>> meta::pure::functions::boolean::tests::and::testTernaryExpressions():Boolean[1]
{
    assert((1 == 1) && (2 != 3) && true);
    assertFalse((1 == 2) && (2 != 3) && false);
}

function <<test.Test>> meta::pure::functions::boolean::tests::and::testShortCircuit():Boolean[1]
{
    let val1 = ^List<String>(values=['Claudius', 'Ptolemy']);
    let val2 = 'Claudius Ptolemy';
    assert($val1->instanceOf(List) && ($val1->cast(@List<String>).values == ['Claudius', 'Ptolemy']));
    assertFalse($val2->instanceOf(List) && ($val2->cast(@List<String>).values == ['Claudius', 'Ptolemy']));
}

function <<test.Test>> meta::pure::functions::boolean::tests::and::testShortCircuitInDynamicEvaluation():Boolean[1]
{
    let fn1 = {|let val1 = ^List<String>(values=['Claudius', 'Ptolemy']);
                $val1->instanceOf(List) && ($val1->cast(@List<String>).values == ['Claudius', 'Ptolemy']);};
    let lambda1 = ^LambdaFunction<{->Boolean[1]}>(expressionSequence = $fn1.expressionSequence);
    assertEquals(true, $lambda1->evaluate([]));

    let fn2 = {|let val2 = 'Claudius Ptolemy';
                $val2->instanceOf(List) && ($val2->cast(@List<String>).values == ['Claudius', 'Ptolemy']);};
    let lambda2 = ^LambdaFunction<{->Boolean[1]}>(expressionSequence = $fn2.expressionSequence);
    assertEquals(false, $lambda2->evaluate([]));
}
